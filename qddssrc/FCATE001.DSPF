     A*%%TS  SD  20140611  120412  LGALHOZ     REL-V6R1M0  5761-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A          R RCATE00100
     A*%%TS  SD  20120224  154940  LGALHOZ     REL-V6R1M0  5761-WDS
     A                                      CF03(03 'Sair')
     A                                      CF12(12 'Cancelar')
     A                                  1 71USER
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A            S#PGM         10A  O  2 71
     A            DS#FNC        57A  O  2 12DSPATR(HI)
     A            DS#PGM        57A  O  1 12DSPATR(HI)
     A          R RCATE00102                SFL
     A*%%TS  SD  20120801  090603  LGALHOZ     REL-V6R1M0  5761-WDS
     A            CDPROD         5Y 0H
     A            OPTION         2A  B  9  2
     A            CDRAMO         6S 0O  9  6
     A            DSPROD        50A  O  9 14
     A            CDSEGURA       3S 0O  9 68
     A          R RCATE00103                SFLCTL(RCATE00102)
     A*%%TS  SD  20121017  154804  LGALHOZ     REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      CF03(03 'Sair')
     A                                      CF05(05 'Mudar')
     A                                      CF06(06 'Gravar')
     A                                      CF12(12 'Cancelar')
     A                                      ROLLUP(50)
     A                                      ROLLDOWN(51)
     A                                      OVERLAY
     A  52                                  SFLDSP
     A                                      SFLDSPCTL
     A  53                                  SFLCLR
     A  54                                  SFLEND(*MORE)
     A                                  8  1'Opt.'
     A                                      COLOR(WHT)
     A            CRITERIA      40A  B  7 15CHGINPDFT(LC)
     A                                      DSPATR(UL)
     A            DSCRITER      12A  O  7  2DSPATR(HI)
     A                                  8  6'Ramo'
     A                                      DSPATR(HI)
     A                                  8 14'Descrição'
     A                                      DSPATR(HI)
     A                                  8 66'Companhia'
     A                                      DSPATR(HI)
     A                                  4  2'Seleccione uma das opções e pressi-
     A                                      one a tecla enter'
     A                                      COLOR(BLU)
     A            DS#OPT01      76   O  5  4COLOR(BLU)
     A            DS#OPT02      76   O  6  4COLOR(BLU)
     A          R RCATE00105
     A*%%TS  SD  20140611  120412  LGALHOZ     REL-V6R1M0  5761-WDS
     A                                      RTNCSRLOC(&CM#NMREC &CM#NMFLD)
     A                                      CSRLOC(CM#ROW     CM#COL)
     A                                      CF03(03 'Sair')
     A                                      CF04(04 'Pop-Up')
     A                                      CF07(07 'Textos')
     A                                      CF08(08 'Cobert.')
     A                                      CF09(09 'Riscos')
     A                                      CF10(10 'Claus.')
     A                                      CF11(11 'Limites')
     A                                      CF12(12 'Cancelar')
     A                                      CF21(21 'Alterar')
     A                                      CF22(22 'Apagar')
     A                                      OVERLAY
     A            CM#ROW         3S 0H
     A            CM#COL         3S 0H
     A            CM#NMREC      10A  H
     A            CM#NMFLD      10A  H
     A                                  4  2'Informação do Produto:'
     A                                      COLOR(BLU)
     A*
     A*---> Código do Programa
     A                                  5  5'Produto....:'
     A*
     A*---> Nome do Programa e Descrição
     A                                  6  5'Ramo/Varia.:'
     A            DSPROD        50A  B  5 25CHGINPDFT(LC)
     A                                      DSPATR(UL)
     A N45                                  DSPATR(PR)
     A  45                                  DSPATR(HI)
     A                                      DSPATR(&F#00)
     A            F#00           1A  P
     A*
     A*---> Tipo de Componente
     A                                 12  5'Alias......:'
     A*
     A*---> Template
     A                                  7  5'Segurador..:'
     A*
     A*---> Comando
     A                                 12 43'Conversão..:'
     A*
     A*---> Acesso por defeito
     A                                  9  5'Periodo....:'
     A            CDRAMO         6Y 0B  6 18EDTWRD('      ')
     A N45                                  DSPATR(PR)
     A  45                                  DSPATR(HI)
     A                                      DSPATR(&F#01)
     A            F#01           1A  P
     A            CDPROD         5S 0O  5 18
     A            CDVARIA        3Y 0B  6 27EDTWRD('   ')
     A N45                                  DSPATR(PR)
     A  45                                  DSPATR(HI)
     A                                      DSPATR(&F#02)
     A            F#02           1A  P
     A            ALIAS         10A  B 12 18
     A N45                                  DSPATR(PR)
     A  45                                  DSPATR(HI)
     A                                      DSPATR(&F#05)
     A            F#05           1A  P
     A            CDSEGURA       3Y 0B  7 18EDTWRD('   ')
     A N45                                  DSPATR(PR)
     A  45                                  DSPATR(HI)
     A                                      DSPATR(&F#03)
     A            F#03           1A  P
     A            CONVERSAO     10A  B 12 56
     A N45                                  DSPATR(PR)
     A  45                                  DSPATR(HI)
     A                                      DSPATR(&F#06)
     A            F#06           1A  P
     A            CAT10001       8Y 0O  9 18EDTWRD('    /  /  ')
     A            CAT10002       8Y 0O  9 31EDTWRD('    /  /  ')
     A            DSSEGURA      40A  O  7 25
     A                                 11  2'Compatibilidade com outros Sistema-
     A                                      s:'
     A                                      COLOR(BLU)
     A                                  6 25'/'
     A                                  9 29'a'
     A                                  9 43'Versão.....:'
     A            NUVERSAO       5Y 2O  9 56EDTWRD('   ,  ')
     A                                  8  5'Programa...:'
     A            CDFUNCT        5Y 0B  8 18EDTWRD('     ')
     A N45                                  DSPATR(PR)
     A  45                                  DSPATR(HI)
     A                                      DSPATR(&F#04)
     A            F#04           1A  P
     A            DSFUNCT       40A  O  8 25
     A                                 15  5'Migrar.....:'
     A                                 14  2'Migrar do antigo para o novo Siste-
     A                                      ma:'
     A                                      COLOR(BLU)
     A            ICMIGRAR       1A  B 15 18
     A N45                                  DSPATR(PR)
     A  45                                  DSPATR(HI)
     A                                      DSPATR(&F#07)
     A            F#07           1A  P
     A                                 15 20'(Sim/Não)'
     A          R RCATE00198
     A*%%TS  SD  20120801  093808  LGALHOZ     REL-V6R1M0  5761-WDS
     A                                      CF03(03 'Sair')
     A                                      CF06(06 'Pop-Up')
     A                                      CF12(12 'Cancelar')
     A                                      OVERLAY
     A                                 12  9'|   NÃO EXISTEM REGISTOS, POR FAVO-
     A                                      R PRESSIONE A   |'
     A                                      DSPATR(HI)
     A                                 11  9'+----------------------------------
     A                                      ----------------+'
     A                                      DSPATR(HI)
     A                                 13  9'|       TECLA F06 PARA ADICIONAR U-
     A                                      M REGISTO       |'
     A                                      DSPATR(HI)
     A                                 14  9'+----------------------------------
     A                                      ----------------+'
     A                                      DSPATR(HI)
     A          R RCATE00199
     A*%%TS  SD  20120713  115338  LGALHOZ     REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A            DS#KEY01      78   O 22  2COLOR(BLU)
     A            DS#KEY02      78   O 23  2COLOR(BLU)
     A            ERR#MSG       78   O 24  2DSPATR(HI)
