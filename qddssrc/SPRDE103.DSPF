     A*%%TS  SD  20140513  155011  LGALHOZ     REL-V6R1M0  5761-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A          R RPRDE10300
     A*%%TS  SD  20120224  154940  LGALHOZ     REL-V6R1M0  5761-WDS
     A                                      CF03(03 'Sair')
     A                                      CF12(12 'Cancelar')
     A                                  1 71USER
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A            S#PGM         10A  O  2 71
     A            DS#FNC        57A  O  2 12DSPATR(HI)
     A            DS#PGM        57A  O  1 12DSPATR(HI)
     A          R RPRDE10305
     A*%%TS  SD  20140513  155011  LGALHOZ     REL-V6R1M0  5761-WDS
     A                                      RTNCSRLOC(*WINDOW &CM#CROW &CM#CCOL)
     A                                      RTNCSRLOC(&CM#NMREC &CM#NMFLD)
     A                                      CSRLOC(CM#ROW     CM#COL)
     A                                      CF03(03 'Sair')
     A                                      CF04(04 'Pop-Up')
     A                                      CF12(12 'Cancelar')
     A                                      CF21(21 'Cancelar')
     A                                      CF22(22 'Cancelar')
     A                                      OVERLAY
     A            CM#CROW        3S 0H
     A            CM#ROW         3S 0H
     A            CM#CCOL        3S 0H
     A            CM#COL         3S 0H
     A            CM#NMREC      10A  H
     A            CM#NMFLD      10A  H
     A                                  4  2'Apólice:'
     A                                      DSPATR(HI)
     A            RAMO           2S 0O  4 11
     A            APOLICE        7S 0O  4 14
     A            NOMSEG        40A  O  4 22
     A                                  8  2'Detalhe do Imóvel:'
     A                                      COLOR(BLU)
     A            DSRISC001     19A  O  5  2DSPATR(HI)
     A            DSRISC002     19A  O  5 40DSPATR(HI)
     A            DSRISC003     19A  O  6  2DSPATR(HI)
     A            CPRISC001     11Y 2O  5 22EDTWRD('   .   .   ,  -')
     A            CPRISC002     11Y 2O  5 60EDTWRD('   .   .   ,  -')
     A            CPRISC003     11Y 2O  6 22EDTWRD('   .   .   ,  -')
     A                                  9  6'Cap.Seguro:'
     A            CPSEGURO      11Y 2B  9 18EDTWRD('   .   .   ,  -')
     A N45                                  DSPATR(PR)
     A  45                                  DSPATR(HI)
     A                                      CHECK(RB)
     A                                      DSPATR(&F#00)
     A            F#00           1A  P
     A                                 10  6'Segurado..:'
     A            CDSEGURA       6Y 0B 10 18
     A N45                                  DSPATR(PR)
     A  45                                  DSPATR(HI)
     A                                      EDTWRD('      ')
     A                                      DSPATR(&F#01)
     A            F#01           1A  P
     A            NOSEGURA      40A  O 10 27
     A                                 11  6'Morada....:'
     A            MORADA        59A  B 11 18
     A N45                                  DSPATR(PR)
     A  45                                  DSPATR(HI)
     A                                      CHECK(LC)
     A                                      DSPATR(&F#02)
     A            F#02           1A  P
     A                                 12  6'Cod.Postal:'
     A            CDPOSTAL       7Y 0B 12 18
     A N45                                  DSPATR(PR)
     A  45                                  DSPATR(HI)
     A                                      CHECK(RB)
     A                                      EDTWRD('    -   ')
     A                                      DSPATR(&F#03)
     A            F#03           1A  P
     A            DSPOSTAL      40A  O 12 27
     A                                 13  6'Localidade:'
     A            LOCAL         25A  B 13 18
     A N45                                  DSPATR(PR)
     A  45                                  DSPATR(HI)
     A                                      CHECK(LC)
     A                                      DSPATR(&F#04)
     A            F#04           1A  P
     A                                 15  2'Informação do Credor Hipotecário:'
     A                                      COLOR(BLU)
     A                                 16  6'Credor....:'
     A            CDCRED         5Y 0B 16 18
     A N45                                  DSPATR(PR)
     A  45                                  DSPATR(HI)
     A                                      EDTWRD('     ')
     A                                      DSPATR(&F#05)
     A            F#05           1A  P
     A            NOCRED        40A  O 16 27
     A                                 18  2'Comentários:'
     A                                      COLOR(BLU)
     A            DSOBS01       71A  B 19  6
     A N45                                  DSPATR(PR)
     A  45                                  DSPATR(HI)
     A                                      CHECK(LC)
     A            DSOBS02       71A  B 20  6
     A N45                                  DSPATR(PR)
     A  45                                  DSPATR(HI)
     A                                      CHECK(LC)
     A          R RPRDE10399
     A*%%TS  SD  20120802  130331  LGALHOZ     REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A            DS#KEY01      78   O 22  2COLOR(BLU)
     A            DS#KEY02      78   O 23  2COLOR(BLU)
     A            ERR#MSG       78   O 24  2COLOR(RED)
