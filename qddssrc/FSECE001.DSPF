     A*%%TS  SD  20140526  092757  LGALHOZ     REL-V6R1M0  5761-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A          R RSECE00100
     A*%%TS  SD  20120224  154940  LGALHOZ     REL-V6R1M0  5761-WDS
     A                                      CF03(03 'Sair')
     A                                      CF12(12 'Cancelar')
     A                                  1 71USER
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                  2  2TIME
     A            S#PGM         10A  O  2 71
     A            DS#FNC        57A  O  2 12DSPATR(HI)
     A            DS#PGM        57A  O  1 12DSPATR(HI)
     A          R RSECE00102                SFL
     A*%%TS  SD  20121017  162341  LGALHOZ     REL-V6R1M0  5761-WDS
     A            CDPROGR        5Y 0H
     A            OPTION         2A  B  9  2
     A            NMPROGR       10A  O  9  6
     A            DSPROGR       40A  O  9 17
     A            CDOMIN         3A  O  9 60
     A            CMITEM         6A  O  9 68
     A            CDTIPO         1A  O  9 77
     A          R RSECE00103                SFLCTL(RSECE00102)
     A*%%TS  SD  20121017  162341  LGALHOZ     REL-V6R1M0  5761-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      CF03(03 'Sair')
     A                                      CF05(05 'Mudar')
     A                                      CF06(06 'Gravar')
     A                                      CF12(12 'Cancelar')
     A                                      ROLLUP(50)
     A                                      ROLLDOWN(51)
     A                                      OVERLAY
     A  52                                  SFLDSP
     A                                      SFLDSPCTL
     A  53                                  SFLCLR
     A  54                                  SFLEND(*MORE)
     A                                  8  1'Opt.'
     A                                      COLOR(WHT)
     A                                  8  6'Nome      '
     A                                      COLOR(WHT)
     A            CRITERIA      40A  B  7 14CHGINPDFT(LC)
     A                                      DSPATR(UL)
     A            DSCRITER      12A  O  7  1DSPATR(HI)
     A                                  8 17'Descrição'
     A                                      DSPATR(HI)
     A                                  8 58'Dominio'
     A                                      DSPATR(HI)
     A                                  8 76'Tipo'
     A                                      DSPATR(HI)
     A                                  8 68'Comando'
     A                                      DSPATR(HI)
     A                                  4  2'Seleccione uma das opções e pressi-
     A                                      one a tecla enter'
     A                                      COLOR(BLU)
     A            DS#OPT01      76   O  5  4COLOR(BLU)
     A            DS#OPT02      76   O  6  4COLOR(BLU)
     A          R RSECE00105
     A*%%TS  SD  20140526  092757  LGALHOZ     REL-V6R1M0  5761-WDS
     A                                      RTNCSRLOC(&CM#NMREC &CM#NMFLD)
     A                                      CSRLOC(CM#ROW     CM#COL)
     A                                      CF03(03 'Sair')
     A                                      CF04(04 'Pop-Up')
     A                                      CF08(08 'Funções')
     A                                      CF09(09 'USERS')
     A                                      CF12(12 'Cancelar')
     A                                      CF21(21 'Alterar')
     A                                      CF22(22 'Apagar')
     A                                      OVERLAY
     A            CM#ROW         3S 0H
     A            CM#COL         3S 0H
     A            CM#NMREC      10A  H
     A            CM#NMFLD      10A  H
     A                                  4  2'Informação do Componente:'
     A                                      COLOR(BLU)
     A*
     A*---> Código do Programa
     A                                  5  5'Código.....:'
     A            CDPROGR        5Y 0O  5 18EDTWRD('     ')
     A*
     A*---> Nome do Programa e Descrição
     A                                  6  5'Nome.......:'
     A            NMPROGR       10A  B  6 18
     A N45                                  DSPATR(PR)
     A  45                                  DSPATR(HI)
     A                                      DSPATR(&F#00)
     A            F#00           1A  P
     A            DSPROGR       40A  B  6 29
     A N45                                  DSPATR(PR)
     A  45                                  DSPATR(HI)
     A                                      CHECK(LC)
     A*
     A*---> Dominio
     A                                  8  5'Dominio....:'
     A            CDOMIN         3A  B  8 18
     A N45                                  DSPATR(PR)
     A  45                                  DSPATR(HI)
     A                                      DSPATR(&F#02)
     A            F#02           1A  P
     A            DDOMIN        40A  O  8 29
     A*
     A*---> Tipo de Componente
     A                                 10  5'Tipo.......:'
     A            CDTIPO         1A  B 10 18
     A N45                                  DSPATR(PR)
     A  45                                  DSPATR(HI)
     A                                      DSPATR(&F#03)
     A            F#03           1A  P
     A            DSTIPO        40A  O 10 29
     A*
     A*---> Template
     A                                 11  5'Template...:'
     A            CDTMPLT        5Y 0B 11 18EDTWRD('     ')
     A N45                                  DSPATR(PR)
     A  45                                  DSPATR(HI)
     A                                      DSPATR(&F#04)
     A            F#04           1A  P
     A            DSTMPLT       40A  O 11 29
     A*
     A*---> Comando
     A                                 12  5'Comando....:'
     A            CMITEM         6A  B 12 18
     A N45                                  DSPATR(PR)
     A  45                                  DSPATR(HI)
     A                                      DSPATR(&F#05)
     A            F#05           1A  P
     A*
     A*---> Acesso por defeito
     A                                 16  5'Acesso.....:'
     A            DFACESS        1A  B 16 18
     A N45                                  DSPATR(PR)
     A  45                                  DSPATR(HI)
     A                                      DSPATR(&F#08)
     A            F#08           1A  P
     A            DSACESS       40A  O 16 29
     A*
     A*---> Biblioteca
     A                                  7  5'Biblioteca.:'
     A            CDLIBL         5Y 0B  7 18EDTWRD('     ')
     A N45                                  DSPATR(PR)
     A  45                                  DSPATR(HI)
     A                                      DSPATR(&F#01)
     A            F#01           1A  P
     A            DSLIBL        40A  O  7 29
     A                                 15  5'Bloqueio...:'
     A            CDBLOCK        5Y 0B 15 18EDTWRD('     ')
     A N45                                  DSPATR(PR)
     A  45                                  DSPATR(HI)
     A                                      DSPATR(&F#07)
     A            F#07           1A  P
     A            DSBLOCK       40A  O 15 29
     A                                 13  5'Frame......:'
     A            NOFRAME       10A  B 13 18CHECK(LC)
     A N45                                  DSPATR(PR)
     A  45                                  DSPATR(HI)
     A                                      DSPATR(&F#06)
     A            F#06           1A  P
     A          R RSECE00198
     A*%%TS  SD  20120801  093808  LGALHOZ     REL-V6R1M0  5761-WDS
     A                                      CF03(03 'Sair')
     A                                      CF06(06 'Pop-Up')
     A                                      CF12(12 'Cancelar')
     A                                      OVERLAY
     A                                 12  9'|   NÃO EXISTEM REGISTOS, POR FAVO-
     A                                      R PRESSIONE A   |'
     A                                      DSPATR(HI)
     A                                 11  9'+----------------------------------
     A                                      ----------------+'
     A                                      DSPATR(HI)
     A                                 13  9'|       TECLA F06 PARA ADICIONAR U-
     A                                      M REGISTO       |'
     A                                      DSPATR(HI)
     A                                 14  9'+----------------------------------
     A                                      ----------------+'
     A                                      DSPATR(HI)
     A          R RSECE00199
     A*%%TS  SD  20120713  115338  LGALHOZ     REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A            DS#KEY01      78   O 22  2COLOR(BLU)
     A            DS#KEY02      78   O 23  2COLOR(BLU)
     A            ERR#MSG       78   O 24  2DSPATR(HI)
